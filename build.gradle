plugins {
    id "com.github.node-gradle.node" version "3.1.1"
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.hivein'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

node {
    version = "16.10.0"
    download = true
    // Set the working directory for unpacking node
    workDir = file("${project.projectDir}/src/main/react/nodejs")

    // Set the work directory for npn
    npmWorkDir = file("${project.projectDir}/src/main/react/npm")

    // Set the work directory where the node modules should be located
    npmWorkDir = file("${project.projectDir}/src/main/react")
}

task npmInstallDependencies(type: NpmTask) {
    dependsOn 'npmSetup'
    execOverrides {
        it.workingDir = 'src/main/react'
    }
    args = ['install']
}

task npmBuild(type: NpmTask) {
    dependsOn 'npmInstallDependencies'
    execOverrides {
        it.workingDir = 'src/main/react'
    }
    args = ['run', 'build']
}

processResources {
    dependsOn 'copyFrontendToBuild'
}

task copyFrontendToBuild(type: Copy) {
    dependsOn 'npmBuild'
    from "$projectDir/src/main/react/build"
    into "$buildDir/resources/main/static"
}
